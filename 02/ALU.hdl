// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // Zeroing x if zx == 1
    Mux16(a=x, b=false, sel=zx, out=xZeroed);

    // Negating x if nx == 1
    Not16(in=xZeroed, out=notX);
    Mux16(a=xZeroed, b=notX, sel=nx, out=xProcessed);

    // Zeroing y if zy == 1
    Mux16(a=y, b=false, sel=zy, out=yZeroed);

    // Negating y if ny == 1
    Not16(in=yZeroed, out=notY);
    Mux16(a=yZeroed, b=notY, sel=ny, out=yProcessed);

    // Function: AND or ADD
    And16(a=xProcessed, b=yProcessed, out=andOut);
    Add16(a=xProcessed, b=yProcessed, out=addOut);
    Mux16(a=andOut, b=addOut, sel=f, out=fOut);

    // Optional negation of output
    Not16(in=fOut, out=notOut);
    Mux16(a=fOut, b=notOut, sel=no, out=tout, out[0..7]=n1,out[8..15]=n2,out[15]=Ng);

    // zr flag
    Or8Way(in=n1, out=orLow);
    Or8Way(in=n2, out=orHigh);
    Or(a=orLow, b=orHigh, out=orAll);
    Not(in=orAll, out=zr);

    // ng flag
    And(a=Ng, b=true, out=ng);

    // Final assignment to output pin
    Mux16(a=tout, b=tout, sel=false, out=out);

}